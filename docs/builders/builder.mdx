# OLVM Builder

The OLVM builder creates OLVM templates from either a source template or a source disk image. This may include, for example, official Oracle Linux OVA templates provided by Oracle, or cloud disk images provided by many Linux distributions (Ubuntu, RockyLinux, etc.).

## Features

- Creates a Packer build VM from an OLVM template or disk image
- Configures SSH access and networking, and supports static IPs
- Generate and optionally export template artifacts (in OVA format) for distribution
- Uses cloud-init for network and SSH authorized keys configuration
- Automatic VM cleanup and template creation
- Support for both template-based and disk-based source images

## Configuration

The OLVM builder supports the following configuration options:

### Required Configuration

#### OLVM Configuration

- `olvm_url` (string) - The URL of the OLVM API endpoint
- `username` (string) - Username for OLVM authentication
- `password` (string) - Password for OLVM authentication

#### Source Configuration (any one of the following)

- `source_template_name` (string) - Name of the source template
- `source_template_id` (string) - ID of the source template (alternative to source_template_name)
- `source_disk_name` (string) - Name of the source disk image
- `source_disk_id` (string) - ID of the source disk image (alternative to source_disk_name)

### Optional Configuration

#### Source Configuration

- `source_template_version` (int) - Version of the source template. Defaults to `1`.
- `cluster` (string) - OLVM cluster name. Defaults to `"Default"`.

#### VM Configuration

- `vm_name` (string) - Name for the VM. Defaults to `"packer-<time-ordered-uuid>"`.
- `vm_vcpu_count` (int) - Number of virtual CPUs. Defaults to `1`.
- `vm_memory_mb` (int) - Memory in MB. Defaults to `1024`.
- `vm_storage_driver` (string) - Storage interface type. Must be one of: `"virtio-scsi"` (default), `"virtio"`.

#### Network Configuration

- `network_name` (string) - Name of the OLVM network to attach to the VM
- `vnic_profile` (string) - vNIC profile to use for the network interface (defaults to `network_name` if not specified)
- `dns_servers` (list of strings) - List of DNS server IP addresses to configure on the VM via cloud-init
- `os_interface_name` (string) - Operating system network interface name. Defaults to `"eth0"`.
- `address` (string) - Static IP address for the VM
- `netmask` (string) - Network mask. Defaults to `"255.255.255.0"`.
- `gateway` (string) - Gateway address

> **Note:** For template-based builds, if the source template already has network interfaces configured, the plugin will configure the first existing interface with the specified `network_name` and `vnic_profile`. If no network interfaces exist, a new one will be created. For disk-based builds, a new network interface is always created.

#### Template Creation

- `destination_template_name` (string) - Name for the generated template. Optional.
- `destination_template_description` (string) - Description for the template. Defaults to `"Template created by Packer from VM <vm_name>"`.

#### Cleanup Configuration

- `cleanup_vm` (bool) - Whether to delete the VM after template creation. Defaults to `true`.
- `cleanup_interfaces` (bool) - Whether to remove network interfaces before template creation. Defaults to `true`.

#### Export Configuration

- `export_host` (string) - Host to export the template to
- `export_directory` (string) - Directory on the export host to save the template. Defaults to `"/tmp"`.
- `export_file_name` (string) - Filename for the exported OVA file. Defaults to `"<destination_template_name>.ova"`.

#### SSH Configuration

- `ssh_username` (string) - SSH username
- `ssh_timeout` (string) - SSH connection timeout. Defaults to `"5m"`.
- `ssh_handshake_attempts` (int) - Number of SSH handshake attempts. Defaults to `10`.

#### TLS Configuration

- `tls_insecure` (bool) - Skip TLS verification. Defaults to `false`.

## Example Usage

### Template-based Build

```hcl
source "olvm" "template-example" {
  # OLVM Configuration
  olvm_url = "https://olvm.example.com/ovirt-engine/api"
  username = "admin@internal"
  password = "password"
  tls_insecure = true

  # Source Configuration
  source_template_name = "oracle-linux-8-template"
  cluster = "Default"

  # Network Configuration
  network_name = "ovirtmgmt"
  vnic_profile = "ovirtmgmt"
  dns_servers   = ["8.8.8.8", "8.8.4.4"]
  os_interface_name = "ens3"  # For systems using predictable network interface names
  
  # VM Configuration
  vm_name       = "packer-test-vm"
  # vm_vcpu_count and vm_memory_mb are optional (default to 1 CPU, 1024MB)
  vm_vcpu_count = 2
  vm_memory_mb  = 4096
  
  # SSH Configuration
  ssh_username = "root"
  ssh_timeout  = "30m"
  
  # Template Configuration
  destination_template_name        = "my-custom-template"
  destination_template_description = "Template created by Packer"
  
  # Cleanup Configuration
  cleanup_vm         = true
  cleanup_interfaces = true
  
  # Export Configuration (optional)
  export_host      = "export.example.com"
  export_directory = "/exports"
  export_file_name = "my-template.ova"
}

build {
  sources = ["source.olvm.template-example"]
}
```

### Disk-based Build

```hcl
source "olvm" "disk-example" {
  # OLVM Configuration
  olvm_url = "https://olvm.example.com/ovirt-engine/api"
  username = "admin@internal"
  password = "password"
  tls_insecure = true

  # Source Configuration
  source_disk_name = "ubuntu-22.04-cloud-disk"
  cluster = "Default"

  # Network Configuration
  network_name = "ovirtmgmt"
  vnic_profile = "ovirtmgmt"
  dns_servers   = ["8.8.8.8", "8.8.4.4"]
  os_interface_name = "ens3"
  
  # VM Configuration
  vm_name       = "packer-ubuntu-vm"
  # vm_vcpu_count and vm_memory_mb are optional (default to 1 CPU, 1024MB)
  vm_vcpu_count = 2
  vm_memory_mb  = 4096
  vm_storage_driver = "virtio-scsi"
  
  # SSH Configuration
  ssh_username = "ubuntu"
  ssh_timeout  = "30m"
  
  # Template Configuration
  destination_template_name = "ubuntu-22.04-template"
  
  # Cleanup Configuration
  cleanup_vm         = true
  cleanup_interfaces = true
}

build {
  sources = ["source.olvm.disk-example"]
}
```

## Environment Variables

The following environment variables can be used instead of configuration options:

- `OLVM_URL` - OLVM API URL
- `OLVM_USERNAME` - OLVM username
- `OLVM_PASSWORD` - OLVM password

## Requirements

- [packer-plugin-sdk](https://github.com/hashicorp/packer-plugin-sdk) >= v0.5.2
- [Go](https://golang.org/doc/install) >= 1.20
- OLVM/oVirt environment with API access

## Packer Compatibility

This plugin is compatible with Packer >= v1.10.2


```hcl
 source "olvm" "example" {
   olvm_url = "https://olvm.example.com/ovirt-engine/api"
   username = "admin@internal"
   password = "password"
   
   source_template_name    = "centos-8-template"
   source_template_version = 1
   
   vm_name = "packer-example-vm"
   cluster = "Default"
   
   address      = "192.168.1.100"
   netmask      = "255.255.255.0"
   gateway      = "192.168.1.1"
   network_name = "ovirtmgmt"
   vnic_profile = "ovirtmgmt"
   
   dns_servers = ["8.8.8.8", "8.8.4.4"]
   os_interface_name = "ens3"  # For systems using predictable network interface names
   
   destination_template_name        = "my-custom-template"
   destination_template_description = "Custom template created by Packer"
   
   ssh_username = "root"
   ssh_timeout  = "30m"
 }

 build {
   sources = ["source.olvm.example"]
   
   provisioner "shell" {
     inline = [
       "echo 'Hello from OLVM!'",
       "yum update -y"
     ]
   }
 }
```


